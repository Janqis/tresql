// each line consists of statement to be tested. line format is following:
//<statement> --> [<semicolon separated bind params (array elements are comma separated)>] --> <result as returned from method uniso.query.result.Jsonizer.jsonize(..., Jsonizer.Arrays)>
//INSERT
//DEPTS
DEPT {DEPTNO, DNAME, LOC} + [10, "ACCOUNTING", "NEW YORK"], [20, "RESEARCH", "DALLAS"] --> 2
DEPT {DEPTNO, DNAME, LOC} + [], [*] --> 30; 'SALES; 'CHICAGO; 40; 'OPERATIONS; 'BOSTON --> 2
//EMPS
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7839, "KING",   "PRESIDENT", null, "1981-11-17", 5000, null, 10] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7698, "BLAKE",  "MANAGER",   7839, "1981-05-01",  2850, null, *] --> 30 --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7782, "CLARK",  *,   7839, "1981-06-9", 2450, null, 10] --> 'MANAGER --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [*, "JONES",  "MANAGER",   7839, "1981-04-02",  2975, null, 20] --> 7566 --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7499, "ALLEN",  "SALESMAN",  7698, "1981-02-20", *] --> 1600; 300; 30 --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7521, *,  7698, "1981-02-22", 1250,  500, 30] --> 'WARD; 'SALESMAN --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [*] --> 7654; 'MARTIN; 'SALESMAN; 7698; '1981-09-28; 1250; 1400; 30 --> 1
EMP + [7844, *] --> 'TURNER; 'SALESMAN;  7698; '1981-09-8; 1500; 0; 30 --> 1
EMP + [] --> 7900; 'JAMES; 'CLERK; 7698; '1981-12-3; 950; null; 30 --> 1
+EMP --> 7934; 'MILLER; 'CLERK; 7782; '1982-01-23; 1300; null; 10 --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7788, "SCOTT",  "ANALYST", 7566, "1982-12-09", 3000, null, 20], [7902, "FORD", "ANALYST", 7566, "1981-12-3", 3000, null, 20], [7876, "ADAMS", "CLERK", 7788, "1983-01-12", 1100, null, 20] --> 3
//insert table with alias
+EMP E {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} --> 7369; 'SMITH; 'CLERK; 7902; 1980-12-17; 800; null; 20 --> 1

//SALGRADES
SALGRADE + [1, 700, 1200],[2, 1201, 1400],[3, 1401, 2000],[4, 2001, 3000],[5, 3001, 9999] --> 5

//WORK
WORK {WDATE, EMPNO, HOURS, EMPNO_MGR} + ["2012-06-06", 7839, 3, null], ["2012-06-07", 7839, 4, null], ["2012-06-06", 7788, 5, 7566], ["2012-06-07", 7788, 8, 7782] --> 4

//CAR
CAR {NR, NAME, IS_ACTIVE, DEPTNR} + [1111, "PORCHE", false, 10], [2222, "BMW", true, 20], [3333, "MERCEDES", false, 20], [4444, "VOLKSWAGEN", null, null] --> 4

//TYRES
TYRES + [1, 3333, "MICHELIN", "S"], [2, 3333, "NOKIAN", "W"] --> 2

//CAR TYRES
CAR[3333] {TYRES_NR} = [2] --> 1

//DUMMY
DUMMY {DUMMY} + [0] --> 1

//----SELECT----
dummy --> [[0]]
(dummy) {*} ++ (dummy) {*} --> [[0], [0]]
(dummy ++ dummy){count(dummy)} --> [[2]] 
//distinct count
(dummy ++ dummy){count(# dummy)} --> [[1]]

emp {count(*)} --> [[14]]
emp e/dept d [d.deptno = 20]{empno, ename, dname}#(empno) --> [[7369, "SMITH", "RESEARCH"], [7566, "JONES", "RESEARCH"], [7788, "SCOTT", "RESEARCH"], [7876, "ADAMS", "RESEARCH"], [7902, "FORD", "RESEARCH"]]
emp e[e.deptno d1, e.deptno d2]dept{d1.deptno, d2.deptno, e.empno}#(1,2,3) --> [[10, 10, 7782], [10, 10, 7839], [10, 10, 7934], [20, 20, 7369], [20, 20, 7566], [20, 20, 7788], [20, 20, 7876], [20, 20, 7902], [30, 30, 7499], [30, 30, 7521], [30, 30, 7654], [30, 30, 7698], [30, 30, 7844], [30, 30, 7900]]
car [is_active = true] {nr, name} #(1) --> [["2222", "BMW"]]
car [is_active = ?] {nr, name} #(1) --> false --> [["1111", "PORCHE"], ["3333", "MERCEDES"]]

//search by primary key shortcut syntax with named variable
dept[:id] {deptno} #(deptno) --> id=10 --> [[10]]
dept[:'id'] {deptno} #(deptno) --> id=10 --> [[10]]
dept[:"id"] {deptno} #(deptno) --> id=10 --> [[10]]

//default join
emp e/dept[sal >= losal & sal <= hisal]salgrade {ename, dname, grade} #(empno) --> [["SMITH", "RESEARCH", 1], ["ALLEN", "SALES", 3], ["WARD", "SALES", 2], ["JONES", "RESEARCH", 4], ["MARTIN", "SALES", 2], ["BLAKE", "SALES", 4], ["CLARK", "ACCOUNTING", 4], ["SCOTT", "RESEARCH", 4], ["KING", "ACCOUNTING", 5], ["TURNER", "SALES", 3], ["ADAMS", "RESEARCH", 1], ["JAMES", "SALES", 1], ["FORD", "RESEARCH", 4], ["MILLER", "ACCOUNTING", 2]]
work[empno]emp/dept {wdate, ename, dname} #(1,2,3) --> [["2012-06-06", "KING", "ACCOUNTING"], ["2012-06-06", "SCOTT", "RESEARCH"], ["2012-06-07", "KING", "ACCOUNTING"], ["2012-06-07", "SCOTT", "RESEARCH"]]
dept/car[2222]#(nr) --> [[20, "RESEARCH", "DALLAS", "2222", "BMW", true, 20, null]]
car/dept[20]#(nr) --> [["2222", "BMW", true, 20, null, 20, "RESEARCH", "DALLAS"], ["3333", "MERCEDES", false, 20, 2, 20, "RESEARCH", "DALLAS"]]
car[2222]/dept[dname != null]#(nr) --> [["2222", "BMW", true, 20, null, 20, "RESEARCH", "DALLAS"]]
tyres/car {name, brand}#(1,2) --> [["MERCEDES", "MICHELIN"], ["MERCEDES", "NOKIAN"]]
car c/tyres t{c.name, t.brand, t.season}#(1) --> [["BMW", null, null], ["MERCEDES", "NOKIAN", "W"], ["PORCHE", null, null], ["VOLKSWAGEN", null, null]]
emp e/emp m {e.empno, e.ename, m.empno, m.ename}#(1) --> [[7369, "SMITH", 7902, "FORD"], [7499, "ALLEN", 7698, "BLAKE"], [7521, "WARD", 7698, "BLAKE"], [7566, "JONES", 7839, "KING"], [7654, "MARTIN", 7698, "BLAKE"], [7698, "BLAKE", 7839, "KING"], [7782, "CLARK", 7839, "KING"], [7788, "SCOTT", 7566, "JONES"], [7839, "KING", null, null], [7844, "TURNER", 7698, "BLAKE"], [7876, "ADAMS", 7788, "SCOTT"], [7900, "JAMES", 7698, "BLAKE"], [7902, "FORD", 7566, "JONES"], [7934, "MILLER", 7782, "CLARK"]]

//foreign key default join
emp/car[2222]{ename, name}#(1,2) --> [["ADAMS", "BMW"], ["FORD", "BMW"], ["JONES", "BMW"], ["SCOTT", "BMW"], ["SMITH", "BMW"]]

//default join with additional expression
dept d/[job = 'PRESIDENT']emp? e {dname, ename}#(1, 2 null) --> [["ACCOUNTING", "KING"], ["OPERATIONS", null], ["RESEARCH", null], ["SALES", null]]
dept d[job = 'PRESIDENT']/emp? e {dname, ename}#(1, 2 null) --> [["ACCOUNTING", "KING"], ["OPERATIONS", null], ["RESEARCH", null], ["SALES", null]]
dept d/emp? e [job = 'PRESIDENT']{dname, ename}#(1, 2) --> [["ACCOUNTING", "KING"]]
emp[?]/dept[dname != null]{ename, dname} --> 7369 --> [["SMITH", "RESEARCH"]]
emp/[?]dept[dname != null]{ename, dname} --> 7369 --> [["SMITH", "RESEARCH"]]
emp[?]/dept[?]{ename, dname} --> 7369; 40 --> []

//foreign key alias join shortcut syntax
emp e[e.deptno d]dept {ename, dname} #(ename) --> [["ADAMS", "RESEARCH"], ["ALLEN", "SALES"], ["BLAKE", "SALES"], ["CLARK", "ACCOUNTING"], ["FORD", "RESEARCH"], ["JAMES", "SALES"], ["JONES", "RESEARCH"], ["KING", "ACCOUNTING"], ["MARTIN", "SALES"], ["MILLER", "ACCOUNTING"], ["SCOTT", "RESEARCH"], ["SMITH", "RESEARCH"], ["TURNER", "SALES"],["WARD", "SALES"]]
work w[w.empno e, w.empno_mgr m]emp[m.ename != null] {e.ename, m.ename, hours} #(1,2,3) --> [["SCOTT", "CLARK", 8], ["SCOTT", "JONES", 5]]
//foreign key alias outer shortcut join syntax
work w[w.empno e, w.empno_mgr m?]emp {e.ename, m.ename, hours} #(1,2,3) --> [["KING", null, 3], ["KING", null, 4], ["SCOTT", "CLARK", 8], ["SCOTT", "JONES", 5]]
work w[empno]emp{ename, hours} #(1,2) --> [["KING", 3], ["KING", 4], ["SCOTT", 5], ["SCOTT", 8]]
work w[empno e, empno_mgr m?]emp {e.ename, m.ename, hours} #(1,2,3) --> [["KING", null, 3], ["KING", null, 4], ["SCOTT", "CLARK", 8], ["SCOTT", "JONES", 5]]

//alias join shortcut syntax
emp e[e.empno = w.empno]work w;e/dept [ename = 'SCOTT']{hours, ename, dname} #(hours) --> [[5, "SCOTT", "RESEARCH"], [8, "SCOTT", "RESEARCH"]]
emp[emp.empno = w.empno]work w;emp/dept [ename = 'SCOTT']{hours, ename, dname} #(hours) --> [[5, "SCOTT", "RESEARCH"], [8, "SCOTT", "RESEARCH"]]
work w[w.empno e, w.empno_mgr m]emp;e/dept d;m/dept md {e.ename, d.dname, hours, m.ename, md.dname} #(1,2,3) --> [["KING", "ACCOUNTING", 3, null, null], ["KING", "ACCOUNTING", 4, null, null], ["SCOTT", "RESEARCH", 5, "JONES", "RESEARCH"], ["SCOTT", "RESEARCH", 8, "CLARK", "ACCOUNTING"]]

//left outer join
dept d/emp? e[e.deptno != null]#{d.deptno}#(deptno) --> [[10], [20], [30]]

//right outer join
emp e/?dept d[e.deptno != null]#{d.deptno}#(deptno) --> [[10], [20], [30]]

//column aliases
emp{ename n}#(1) --> [["ADAMS"], ["ALLEN"], ["BLAKE"], ["CLARK"], ["FORD"], ["JAMES"], ["JONES"], ["KING"], ["MARTIN"], ["MILLER"], ["SCOTT"], ["SMITH"], ["TURNER"], ["WARD"]]
emp{ename 'n'}#(1) --> [["ADAMS"], ["ALLEN"], ["BLAKE"], ["CLARK"], ["FORD"], ["JAMES"], ["JONES"], ["KING"], ["MARTIN"], ["MILLER"], ["SCOTT"], ["SMITH"], ["TURNER"], ["WARD"]]
emp{ename "n"}#(1) --> [["ADAMS"], ["ALLEN"], ["BLAKE"], ["CLARK"], ["FORD"], ["JAMES"], ["JONES"], ["KING"], ["MARTIN"], ["MILLER"], ["SCOTT"], ["SMITH"], ["TURNER"], ["WARD"]]
emp{ename "select"}#(1) --> [["ADAMS"], ["ALLEN"], ["BLAKE"], ["CLARK"], ["FORD"], ["JAMES"], ["JONES"], ["KING"], ["MARTIN"], ["MILLER"], ["SCOTT"], ["SMITH"], ["TURNER"], ["WARD"]]
emp{ename 'select'}#(1) --> [["ADAMS"], ["ALLEN"], ["BLAKE"], ["CLARK"], ["FORD"], ["JAMES"], ["JONES"], ["KING"], ["MARTIN"], ["MILLER"], ["SCOTT"], ["SMITH"], ["TURNER"], ["WARD"]]
emp/dept[10] {dname || ',' || ename name}#(1) --> [["ACCOUNTING,CLARK"], ["ACCOUNTING,KING"], ["ACCOUNTING,MILLER"]]

//select in column clause
emp[ename ~~ ?]{ename, (dept[?]{dname}) dname, (dept[?]{dname}) dname1} --> 'KIN%; 10; 20 --> [["KING", "ACCOUNTING", "RESEARCH"]]
dept[?] {dname, (emp [emp.deptno = dept.deptno] {count(*)}) emp_count, |emp {ename}#(1)} --> 10 --> [["ACCOUNTING", 3, [["CLARK"], ["KING"], ["MILLER"]]]]
dept[?] {dname, |emp {ename}#(1), (emp [emp.deptno = dept.deptno] {count(*)}) emp_count} --> 10 --> [["ACCOUNTING", [["CLARK"], ["KING"], ["MILLER"]], 3]]

//order
dept/emp[sal >= losal & sal <= hisal]salgrade {deptno, grade, empno, sal}#(deptno, grade)~#(sal) --> [[10, 2, 7934, 1300.00], [10, 4, 7782, 2450.00], [10, 5, 7839, 5000.00], [20, 1, 7876, 1100.00], [20, 1, 7369, 800.00], [20, 4, 7788, 3000.00], [20, 4, 7902, 3000.00], [20, 4, 7566, 2975.00], [30, 1, 7900, 950.00], [30, 2, 7521, 1250.00], [30, 2, 7654, 1250.00], [30, 3, 7499, 1600.00], [30, 3, 7844, 1500.00], [30, 4, 7698, 2850.00]]
dept/emp[sal >= losal & sal <= hisal]salgrade {deptno, grade, empno, sal}#(null deptno, grade)~#(sal, 3 null) --> [[10, 2, 7934, 1300.00], [10, 4, 7782, 2450.00], [10, 5, 7839, 5000.00], [20, 1, 7369, 800.00], [20, 1, 7876, 1100.00], [20, 4, 7566, 2975.00], [20, 4, 7902, 3000.00], [20, 4, 7788, 3000.00], [30, 1, 7900, 950.00], [30, 2, 7654, 1250.00], [30, 2, 7521, 1250.00], [30, 3, 7844, 1500.00], [30, 3, 7499, 1600.00], [30, 4, 7698, 2850.00]]

//in operator
dept[deptno in(10)]{deptno}#(1) --> [[10]]
dept[deptno in 10]{deptno}#(1) --> [[10]]
dept[deptno !in 10]{deptno}#(1) --> [[20], [30], [40]]
dept[deptno in(10, 20)]{deptno}#(1) --> [[10], [20]]
dept[deptno !in (:id1, :id2)]{deptno}#(1) --> id1=10; id2=20 --> [[30], [40]]
dept[deptno in[10, 20]]{deptno}#(1) --> [[10], [20]]
dept[deptno !in [:id1, :id2]]{deptno}#(1) --> id1=10; id2=20 --> [[30], [40]]

//case when else using dialect as case function
dept{dname, case(deptno in(10, 40), "east") region}#(1) --> [["ACCOUNTING", "east"], ["OPERATIONS", "east"], ["RESEARCH", null], ["SALES", null]]
dept{dname, case(deptno = 20, "south", "other") region}#(2,1) --> [["ACCOUNTING", "other"], ["OPERATIONS", "other"], ["SALES", "other"], ["RESEARCH", "south"]]
dept{dname, case(deptno = 10, trim("east"), deptno=30, "north", deptno=20, "south", "other") region}#(2,1) --> [["ACCOUNTING", "east"], ["SALES", "north"], ["OPERATIONS", "other"], ["RESEARCH", "south"]]

//in subselect
dept [deptno in (emp{deptno})]#(deptno) --> [[10, "ACCOUNTING", "NEW YORK"], [20, "RESEARCH", "DALLAS"], [30, "SALES", "CHICAGO"]]
dept [deptno in (emp[ename ~~ ?]{deptno}) | deptno = 40]#(deptno) --> 'KIN% --> [[10, "ACCOUNTING", "NEW YORK"], [40, "OPERATIONS", "BOSTON"]]

//exists correlated subselect
dept d[(emp e[e.deptno = d.deptno])]#(deptno) --> [[10, "ACCOUNTING", "NEW YORK"], [20, "RESEARCH", "DALLAS"], [30, "SALES", "CHICAGO"]]

//from clause select
dept[deptno in ((dept[:id]){deptno})]{deptno}#(1) --> id=10 --> [[10]]
(dummy)[](dummy {dummy} ++ dummy {dummy})[](dummy) --> [[0, 0, 0], [0, 0, 0]]

//array binding
emp [empno in ?]{empno, ename, job}#(empno) --> [] --> []
emp [empno !in ?]{empno, ename, job}#(empno) --> [] --> []
emp [empno in(?)]{empno, ename, job}#(empno) --> [7839, 7782] --> [[7782, "CLARK", "MANAGER"], [7839, "KING", "PRESIDENT"]]
emp [ename ~~ ? & empno in(?) & mgr = null & sal = ?]{empno, ename, job} --> 'kin%; [7839, 7782]; 5000  --> [[7839, "KING", "PRESIDENT"]]
emp [ename ~~ ? & empno in ? & mgr = null & sal = ?]{empno, ename, job} --> 'kin%; []; 5000  --> []

//named variables
dept[dname ~~ :name] --> name='acc% --> [[10, "ACCOUNTING", "NEW YORK"]]

//mixed mode - named indexed variables
dept[deptno = ? & dname ~~ :name] --> name='acc%;10 --> [[10, "ACCOUNTING", "NEW YORK"]]

//in subselect with binding
dept[deptno in (emp[sal >= ?]{deptno})] {dname} --> 5000 --> [["ACCOUNTING"]]
dept d[d.deptno in (emp e[e.deptno in (dept d1[:id?]{d1.deptno})]{e.deptno})]{deptno}#(deptno) --> id=10 --> [[10]]

//ternary comparison
emp[losal <= sal <= hisal] salgrade {ename, sal, grade} #(3,2,1) --> [["SMITH", 800.00, 1], ["JAMES", 950.00, 1], ["ADAMS", 1100.00, 1], ["MARTIN", 1250.00, 2], ["WARD", 1250.00, 2], ["MILLER", 1300.00, 2], ["TURNER", 1500.00, 3], ["ALLEN", 1600.00, 3], ["CLARK", 2450.00, 4], ["BLAKE", 2850.00, 4], ["JONES", 2975.00, 4], ["FORD", 3000.00, 4], ["SCOTT", 3000.00, 4], ["KING", 5000.00, 5]]
emp/dept[dname ~~ 'SAL%' & ? <= sal < ? & comm != null ]{ename, sal, dname} #(ename) --> 1500; 2000 --> [["ALLEN", 1600.00, "SALES"], ["TURNER", 1500.00, "SALES"]]

//group by, having
emp[sal >= losal & sal <= hisal]salgrade {grade, losal, hisal, count(empno)} (grade, losal, hisal) #(1) --> [[1, 700, 1200, 3], [2, 1201, 1400, 3], [3, 1401, 2000, 2], [4, 2001, 3000, 5], [5, 3001, 9999, 1]]
emp[sal >= losal & sal <= hisal]salgrade {grade, losal, hisal, count(*)} (grade, losal, hisal)^(count(*) > ?)#(1) --> 1 --> [[1, 700, 1200, 3], [2, 1201, 1400, 3], [3, 1401, 2000, 2], [4, 2001, 3000, 5]] 

//two level hierarchical query: salgrade -> emp
salgrade[?] {grade, losal, hisal, |[sal >= :1(2) & sal <= :1(3)]emp{ename, sal}} --> 5 --> [[5, 3001, 9999, [["KING", 5000.00]]]]
salgrade[?] {grade, losal lo, hisal, |[]emp[sal >= :1('lo') & sal <= :1('hisal')]{ename, sal}} --> 5 --> [[5, 3001, 9999, [["KING", 5000.00]]]]
salgrade[?] {grade, losal, hisal hi, |[sal >= :1(losal)]emp[sal <= :1(hi)]{ename, sal}} --> 5 --> [[5, 3001, 9999, [["KING", 5000.00]]]]
car{name, |tyres{brand, season}#(1, 2)}#(1) --> [["BMW", []], ["MERCEDES", [["MICHELIN", "S"], ["NOKIAN", "W"]]], ["PORCHE", []], ["VOLKSWAGEN", []]]
tyres {brand, |car{nr, name}#(1)}#(1) --> [["MICHELIN", []], ["NOKIAN", [["3333", "MERCEDES"]]]]
//join with from clause table in hierarchical query
(dept[10]) d{dname, |emp {ename}#(1)} --> [["ACCOUNTING", [["CLARK"], ["KING"], ["MILLER"]]]]
((dept[10]) d) d{dname, |emp {ename}#(1)} --> [["ACCOUNTING", [["CLARK"], ["KING"], ["MILLER"]]]]

//three level hierarchical query: dept -> salgrade -> emp
dept d{deptno, dname, |emp[sal >= losal & sal <= hisal]salgrade[deptno = :1(1)] {grade, losal, hisal, count(empno) empcount, |[emp.deptno = :2(1)]emp/dept[sal >= :1(2) & sal <= :1(3)]{ename, emp.deptno, dname, sal}#(empno) emps} (grade, losal, hisal) #(1) salgrades}#(deptno) --> [[10, "ACCOUNTING", [[2, 1201, 1400, 1, [["MILLER", 10, "ACCOUNTING", 1300.00]]], [4, 2001, 3000, 1, [["CLARK", 10, "ACCOUNTING", 2450.00]]], [5, 3001, 9999, 1, [["KING", 10, "ACCOUNTING", 5000.00]]]]], [20, "RESEARCH", [[1, 700, 1200, 2, [["SMITH", 20, "RESEARCH", 800.00], ["ADAMS", 20, "RESEARCH", 1100.00]]], [4, 2001, 3000, 3, [["JONES", 20, "RESEARCH", 2975.00], ["SCOTT", 20, "RESEARCH", 3000.00], ["FORD", 20, "RESEARCH", 3000.00]]]]], [30, "SALES", [[1, 700, 1200, 1, [["JAMES", 30, "SALES", 950.00]]], [2, 1201, 1400, 2, [["WARD", 30, "SALES", 1250.00], ["MARTIN", 30, "SALES", 1250.00]]], [3, 1401, 2000, 2, [["ALLEN", 30, "SALES", 1600.00], ["TURNER", 30, "SALES", 1500.00]]], [4, 2001, 3000, 1, [["BLAKE", 30, "SALES", 2850.00]]]]], [40, "OPERATIONS", []]]

//ancestor join - [d.deptno = emp.deptno]
dept d{deptno, dname, |emp[sal >= losal & sal <= hisal]salgrade[deptno = :1(1)] {grade, losal, hisal, count(empno) empcount, |[d.deptno = emp.deptno]emp/dept[sal >= :1(2) & sal <= :1(3)]{ename, emp.deptno, dname, sal}#(empno) emps} (grade, losal, hisal) #(1) salgrades}#(deptno) --> [[10, "ACCOUNTING", [[2, 1201, 1400, 1, [["MILLER", 10, "ACCOUNTING", 1300.00]]], [4, 2001, 3000, 1, [["CLARK", 10, "ACCOUNTING", 2450.00]]], [5, 3001, 9999, 1, [["KING", 10, "ACCOUNTING", 5000.00]]]]], [20, "RESEARCH", [[1, 700, 1200, 2, [["SMITH", 20, "RESEARCH", 800.00], ["ADAMS", 20, "RESEARCH", 1100.00]]], [4, 2001, 3000, 3, [["JONES", 20, "RESEARCH", 2975.00], ["SCOTT", 20, "RESEARCH", 3000.00], ["FORD", 20, "RESEARCH", 3000.00]]]]], [30, "SALES", [[1, 700, 1200, 1, [["JAMES", 30, "SALES", 950.00]]], [2, 1201, 1400, 2, [["WARD", 30, "SALES", 1250.00], ["MARTIN", 30, "SALES", 1250.00]]], [3, 1401, 2000, 2, [["ALLEN", 30, "SALES", 1600.00], ["TURNER", 30, "SALES", 1500.00]]], [4, 2001, 3000, 1, [["BLAKE", 30, "SALES", 2850.00]]]]], [40, "OPERATIONS", []]]
dept d{deptno, dname, |emp[sal >= losal & sal <= hisal]salgrade g {grade, losal, hisal, count(empno) empcount, |[d.deptno = emp.deptno & sal >= g.losal & sal <= g.hisal]emp/dept{ename, emp.deptno, dname, sal}#(empno) emps} (grade, losal, hisal) #(1) salgrades}#(deptno) --> [[10, "ACCOUNTING", [[2, 1201, 1400, 1, [["MILLER", 10, "ACCOUNTING", 1300.00]]], [4, 2001, 3000, 1, [["CLARK", 10, "ACCOUNTING", 2450.00]]], [5, 3001, 9999, 1, [["KING", 10, "ACCOUNTING", 5000.00]]]]], [20, "RESEARCH", [[1, 700, 1200, 2, [["SMITH", 20, "RESEARCH", 800.00], ["ADAMS", 20, "RESEARCH", 1100.00]]], [4, 2001, 3000, 3, [["JONES", 20, "RESEARCH", 2975.00], ["SCOTT", 20, "RESEARCH", 3000.00], ["FORD", 20, "RESEARCH", 3000.00]]]]], [30, "SALES", [[1, 700, 1200, 1, [["JAMES", 30, "SALES", 950.00]]], [2, 1201, 1400, 2, [["WARD", 30, "SALES", 1250.00], ["MARTIN", 30, "SALES", 1250.00]]], [3, 1401, 2000, 2, [["ALLEN", 30, "SALES", 1600.00], ["TURNER", 30, "SALES", 1500.00]]], [4, 2001, 3000, 1, [["BLAKE", 30, "SALES", 2850.00]]]]], [40, "OPERATIONS", []]]
dept d{deptno, dname, |emp[sal >= losal & sal <= hisal]salgrade {grade, count(empno) empcount, |[emp.deptno = d.deptno & sal >= salgrade.losal & sal <= salgrade.hisal]emp/dept{ename, emp.deptno, dname, sal}#(empno) emps} (grade, losal, hisal) #(1) salgrades}#(deptno) --> [[10, "ACCOUNTING", [[2, 1, [["MILLER", 10, "ACCOUNTING", 1300.00]]], [4, 1, [["CLARK", 10, "ACCOUNTING", 2450.00]]], [5, 1, [["KING", 10, "ACCOUNTING", 5000.00]]]]], [20, "RESEARCH", [[1, 2, [["SMITH", 20, "RESEARCH", 800.00], ["ADAMS", 20, "RESEARCH", 1100.00]]], [4, 3, [["JONES", 20, "RESEARCH", 2975.00], ["SCOTT", 20, "RESEARCH", 3000.00], ["FORD", 20, "RESEARCH", 3000.00]]]]], [30, "SALES", [[1, 1, [["JAMES", 30, "SALES", 950.00]]], [2, 2, [["WARD", 30, "SALES", 1250.00], ["MARTIN", 30, "SALES", 1250.00]]], [3, 2, [["ALLEN", 30, "SALES", 1600.00], ["TURNER", 30, "SALES", 1500.00]]], [4, 1, [["BLAKE", 30, "SALES", 2850.00]]]]], [40, "OPERATIONS", []]]
dept[10, 20]{dname, |[dept.deptno = e.deptno](emp) e {ename}#(1) }#(1) --> [["ACCOUNTING", [["CLARK"], ["KING"], ["MILLER"]]], ["RESEARCH", [["ADAMS"], ["FORD"], ["JONES"], ["SCOTT"], ["SMITH"]]]]
(dept[10]) d {dname, |[d.deptno = c.deptnr]car c #(nr)} --> [["ACCOUNTING", [["1111", "PORCHE", false, 10, null]]]]

//two level hierarchical query, child query in the midst of parent query columns
dept[?]{loc, deptno, |emp[deptno = :1(2)] {job}, dname} --> 10 --> [["NEW YORK", 10, [["PRESIDENT"], ["MANAGER"], ["CLERK"]], "ACCOUNTING"]]
//default parent child hierarchical query join
dept[?]{loc, |emp {job}#(1), dname} --> 10 --> [["NEW YORK", [["CLERK"], ["MANAGER"], ["PRESIDENT"]], "ACCOUNTING"]]

//hierarchical query default shortcut join
emp/dept[10]{dname, ename, |[deptno]emp{ename}#(1)} --> [["ACCOUNTING", "CLARK", [["CLARK"], ["KING"], ["MILLER"]]], ["ACCOUNTING", "KING", [["CLARK"], ["KING"], ["MILLER"]]], ["ACCOUNTING", "MILLER", [["CLARK"], ["KING"], ["MILLER"]]]]

//hierarchical query foreign key shortcut syntax
dept/emp[?] {dname, ename, |[empno_mgr]work{hours}#(1)} --> 7566 --> [["RESEARCH", "JONES", [[5]]]]
dept/emp e[?] {dname, ename, |[empno_mgr]work{hours}#(1)} --> 7566 --> [["RESEARCH", "JONES", [[5]]]]

//three level hierarchical query with default and fk shortcut joins
dept[?]{loc, |emp {ename, job, |[empno_mgr]work {hours}#(1) controlled_hours}#(1), dname} --> 20 --> [["DALLAS", [["ADAMS", "CLERK", []], ["FORD", "ANALYST", []], ["JONES", "MANAGER", [[5]]], ["SCOTT", "ANALYST", []], ["SMITH", "CLERK", []]], "RESEARCH"]]

//union
dept[?]{deptno} ++ emp[?]{empno} ++ salgrade[?]{grade} --> 10;7839;1 --> [[10], [7839], [1]]
(dept{deptno} + salgrade{grade}) ~#(1) --> [[40], [30], [20], [10], [5], [4], [3], [2], [1]]

//intersect
dept{deptno} && emp{empno} --> []
dept d[d.deptno = d1.deptno](dept{deptno} && emp[sal >= ?]{deptno}) d1 {deptno, dname} --> 5000 --> [[10, "ACCOUNTING"]]

//except
dept{deptno} - dept/emp?[ename != null]{deptno} --> [[40]]

//product, multiple col sorting
dept[]salgrade{dname, grade}#(1,loc)~#(2) --> [["ACCOUNTING", 5], ["ACCOUNTING", 4], ["ACCOUNTING", 3], ["ACCOUNTING", 2], ["ACCOUNTING", 1], ["OPERATIONS", 5], ["OPERATIONS", 4], ["OPERATIONS", 3], ["OPERATIONS", 2], ["OPERATIONS", 1], ["RESEARCH", 5], ["RESEARCH", 4], ["RESEARCH", 3], ["RESEARCH", 2], ["RESEARCH", 1], ["SALES", 5], ["SALES", 4], ["SALES", 3], ["SALES", 2], ["SALES", 1]]
dept[]salgrade[]dummy{dname, grade, dummy}#(1,loc)~#(2) --> [["ACCOUNTING", 5, 0], ["ACCOUNTING", 4, 0], ["ACCOUNTING", 3, 0], ["ACCOUNTING", 2, 0], ["ACCOUNTING", 1, 0], ["OPERATIONS", 5, 0], ["OPERATIONS", 4, 0], ["OPERATIONS", 3, 0], ["OPERATIONS", 2, 0], ["OPERATIONS", 1, 0], ["RESEARCH", 5, 0], ["RESEARCH", 4, 0], ["RESEARCH", 3, 0], ["RESEARCH", 2, 0], ["RESEARCH", 1, 0], ["SALES", 5, 0], ["SALES", 4, 0], ["SALES", 3, 0], ["SALES", 2, 0], ["SALES", 1, 0]]

//all cols: *
dept[10]{*} --> [[10, "ACCOUNTING", "NEW YORK"]]
dept[10]{*, |[deptno = :1(1)]emp{ename}} --> [[10, "ACCOUNTING", "NEW YORK", [["KING"], ["CLARK"], ["MILLER"]]]]
dept[?]{|[]emp[?]{ename}, *} --> 10;7839 --> [[[["KING"]], 10, "ACCOUNTING", "NEW YORK"]]

//all cols from one table
emp/dept[10] {deptno, dname, emp.*, |[deptno = :1(deptno)]emp{ename}#(ename)}#(empno) --> [[10, "ACCOUNTING", 7782, "CLARK", "MANAGER", 7839, "1981-06-09", 2450.00, null, 10, [["CLARK"], ["KING"], ["MILLER"]]], [10, "ACCOUNTING", 7839, "KING", "PRESIDENT", null, "1981-11-17", 5000.00, null, 10, [["CLARK"], ["KING"], ["MILLER"]]], [10, "ACCOUNTING", 7934, "MILLER", "CLERK", 7782, "1982-01-23", 1300.00, null, 10, [["CLARK"], ["KING"], ["MILLER"]]]]
//NOTE: child query allways stays last in result when all cols from one table is used 
emp/dept[10] {|[]emp[deptno = :1(deptno)]{ename}#(ename), deptno, dname, emp.*}#(empno) --> [[10, "ACCOUNTING", 7782, "CLARK", "MANAGER", 7839, "1981-06-09", 2450.00, null, 10, [["CLARK"], ["KING"], ["MILLER"]]], [10, "ACCOUNTING", 7839, "KING", "PRESIDENT", null, "1981-11-17", 5000.00, null, 10, [["CLARK"], ["KING"], ["MILLER"]]], [10, "ACCOUNTING", 7934, "MILLER", "CLERK", 7782, "1982-01-23", 1300.00, null, 10, [["CLARK"], ["KING"], ["MILLER"]]]]

//bind variables in columns
dept{?} --> 1 --> [["1"], ["1"], ["1"], ["1"]]
dept{:x} --> x = 'y --> [["y"], ["y"], ["y"], ["y"]]

//limit offset
dept#(deptno)@(1 2) --> [[20, "RESEARCH", "DALLAS"], [30, "SALES", "CHICAGO"]]
dept#(deptno)@(? ?) --> 2;3 --> [[30, "SALES", "CHICAGO"], [40, "OPERATIONS", "BOSTON"]]
dept#(deptno)@(1, 2) --> [[20, "RESEARCH", "DALLAS"], [30, "SALES", "CHICAGO"]]
dept#(deptno)@(? , ?) --> 2;3 --> [[30, "SALES", "CHICAGO"], [40, "OPERATIONS", "BOSTON"]]
dept#(deptno)@(3, ) --> [[40, "OPERATIONS", "BOSTON"]]
dept#(deptno)@(1) --> [[10, "ACCOUNTING", "NEW YORK"]]

//single quoted string
dept[dname ~ 'ACC%'] --> [[10, "ACCOUNTING", "NEW YORK"]]

//implicit left outer join
work[empno e, empno_mgr m]emp;e/dept d;m/dept md{hours, e.ename, d.dname, m.ename, md.dname}#(1,2,3) --> [[3, "KING", "ACCOUNTING", null, null], [4, "KING", "ACCOUNTING", null, null], [5, "SCOTT", "RESEARCH", "JONES", "RESEARCH"], [8, "SCOTT", "RESEARCH", "CLARK", "ACCOUNTING"]]
work w[w.empno e, w.empno_mgr m]emp;e/dept d;m/dept md{w.hours, e.ename, d.dname, m.ename, md.dname}#(1,2,3) --> [[3, "KING", "ACCOUNTING", null, null], [4, "KING", "ACCOUNTING", null, null], [5, "SCOTT", "RESEARCH", "JONES", "RESEARCH"], [8, "SCOTT", "RESEARCH", "CLARK", "ACCOUNTING"]]
work[empno_mgr]emp/dept {hours, ename, dname} --> [[3, null, null], [4, null, null], [5, "JONES", "RESEARCH"], [8, "CLARK", "ACCOUNTING"]]
car/dept{deptno, dname, nr, name} #(1,2,3,4) --> [[null, null, "4444", "VOLKSWAGEN"], [10, "ACCOUNTING", "1111", "PORCHE"], [20, "RESEARCH", "2222", "BMW"], [20, "RESEARCH", "3333", "MERCEDES"]]
dept/car{deptno, dname, nr, name} #(1,2,3,4) --> [[10, "ACCOUNTING", "1111", "PORCHE"], [20, "RESEARCH", "2222", "BMW"], [20, "RESEARCH", "3333", "MERCEDES"]]
car[1111]/dept {nr, dname}#(1,2) --> [["1111", "ACCOUNTING"], ["2222", null], ["3333", null], ["4444", null]]
car/[1111]dept {nr, dname}#(1,2) --> [["1111", "ACCOUNTING"], ["2222", null], ["3333", null], ["4444", null]]
car[nr = 1111]/dept {nr, dname}#(1,2) --> [["1111", "ACCOUNTING"], ["2222", null], ["3333", null], ["4444", null]]
car/[nr = 1111]dept {nr, dname}#(1,2) --> [["1111", "ACCOUNTING"], ["2222", null], ["3333", null], ["4444", null]]
work w[w.empno_mgr m]emp {hours, ename}#(1,2) --> [[3, null], [4, null], [5, "JONES"], [8, "CLARK"]]
work w[w.empno_mgr]emp m {hours, ename}#(1,2) --> [[3, null], [4, null], [5, "JONES"], [8, "CLARK"]]
work[work.empno_mgr m]emp {hours, ename}#(1,2) --> [[3, null], [4, null], [5, "JONES"], [8, "CLARK"]]

//optional binding
dept[deptno = :id?] {deptno} #(deptno) --> [[10], [20], [30], [40]]
dept[:id?] {deptno} #(deptno) --> id=10 --> [[10]]
dept[:id?] {deptno} #(deptno) --> [[10], [20], [30], [40]]
dept[:id? & dname ~~ :name? & loc ~~ :loc?] --> loc = 'bost% --> [[40, "OPERATIONS", "BOSTON"]]
dept[(:id? & (dname ~~ :name? & (loc ~~ :loc?)))] --> loc = 'bost% --> [[40, "OPERATIONS", "BOSTON"]]
dept [deptno in (emp[ename ~~ :n?]{deptno})]{deptno}#(deptno) --> [[10], [20], [30], [40]]
dept [deptno in (emp[ename ~~ :n?]{deptno}) | deptno = 40]#(deptno) --> [[40, "OPERATIONS", "BOSTON"]]
dept [deptno in (emp[ename ~~ :n?]{deptno}) | deptno = 40 | deptno in (emp{deptno})]{deptno}#(deptno) --> [[10], [20], [30], [40]]
dept [deptno in (emp[ename ~~ :n?]{deptno})]{deptno}#(deptno),dept [deptno in (emp{deptno})]{deptno}#(deptno) --> [[[10], [20], [30], [40]], [[10], [20], [30]]]
dept d[d.deptno in (emp e[e.deptno in (dept d1[:id?]{d1.deptno})]{e.deptno})]{deptno}#(deptno) --> [[10], [20], [30], [40]]
dept d[d.deptno in (emp e[e.deptno in (dept d1[:id?]{d1.deptno})]{e.deptno}) & d.deptno in (emp e{e.deptno})]{deptno}#(deptno) --> [[10], [20], [30]]
dept[deptno in (/(dept[:id?]{deptno}){deptno})]{deptno}#(1) --> [[10], [20], [30], [40]]
dept[deptno in (/(salgrade[:id?]{grade} + dept[:id?]{deptno}){*})]{deptno}#(1) --> [[10], [20], [30], [40]]
dept[deptno in (/(salgrade[:id?]{grade} + dept[10]{deptno}){*})]{deptno}#(1) --> [[10]]
dept[deptno in ((dept[:id?]{deptno}){deptno})]{deptno}#(1) --> [[10], [20], [30], [40]]
dept[deptno in ((salgrade[:id?]{grade} + dept[:id?]{deptno}){*})]{deptno}#(1) --> [[10], [20], [30], [40]]
dept[deptno in ((salgrade[:id?]{grade} + dept[10]{deptno}){*})]{deptno}#(1) --> [[10]]
dept[deptno in (emp/dept[dname ~~ :n?]{deptno})]{deptno}#(1) --> [[10], [20], [30], [40]]
dept[deptno in (emp/dept[dname ~~ :n?]{deptno})]{deptno}#(1) --> n='acc% --> [[10]]
dept[10 & dname ~upper(:n?)] {dname} --> [["ACCOUNTING"]]
dept[10 & dname ~upper(lower(:n?))] {dname} --> [["ACCOUNTING"]]
  //offset limit optional binding
dept{deptno}#(deptno)@(:o? :l?) --> [[10], [20], [30], [40]]
dept{deptno}#(deptno)@(:o? :l?) --> o=3 --> [[40]]
dept{deptno}#(deptno)@(:o? :l?) --> l=1 --> [[10]]

//distinct
(dept{deptno} ++ dept{deptno})#{deptno}#(1) --> [[10], [20], [30], [40]]

//braces test
dept[deptno = ((? + ?) * ((? - ?) / (?)))] {deptno} --> 2; 3; 6; 2; 2 --> [[10]]
((1 + 2) * (((3 - 4)) / (5))) --> -0.6

//division test (because of language peculiarities since / sign is used also as default join)
emp[](emp{avg(sal) avgsal}) {ename, sal, avgsal, sal/avgsal}#(4, 1) --> [["SMITH", 800.00, 2073.21, 0.38], ["JAMES", 950.00, 2073.21, 0.45], ["ADAMS", 1100.00, 2073.21, 0.53], ["MARTIN", 1250.00, 2073.21, 0.60], ["WARD", 1250.00, 2073.21, 0.60], ["MILLER", 1300.00, 2073.21, 0.62], ["TURNER", 1500.00, 2073.21, 0.72], ["ALLEN", 1600.00, 2073.21, 0.77], ["CLARK", 2450.00, 2073.21, 1.18], ["BLAKE", 2850.00, 2073.21, 1.37], ["JONES", 2975.00, 2073.21, 1.43], ["FORD", 3000.00, 2073.21, 1.44], ["SCOTT", 3000.00, 2073.21, 1.44], ["KING", 5000.00, 2073.21, 2.41]]
emp[](emp{avg(sal) avgsal}) [sal/avgsal <= 1] {count(*)} --> [[8]]
emp[](emp{avg(sal) avgsal}) [(sal)/(avgsal) <= 1] {count(*)} --> [[8]]
emp[](emp{avg(sal) avgsal}) [sal/(avgsal) <= 1] {count(*)} --> [[8]]
emp[](emp{avg(sal) avgsal}) [(sal)/avgsal <= 1] {count(*)} --> [[8]]

//----UPDATE----
emp[hiredate >= ? & hiredate <= ?]{sal} = [sal + ?] --> '1982-01-23;'1982-12-09;1000.35 --> 2
emp[hiredate >= ? & hiredate <= ?]{sal} ~#(1) --> '1982-01-23;'1982-12-09 --> [[4000.35], [2300.35]]
=emp[?] [7876, *, 20] --> 7876; null; 'DESIGNER; null; null; null; null --> 1
emp[7876] --> [[7876, null, "DESIGNER", null, null, null, null, 20]]
=emp[?] --> 7876; 7876; null; null; null; null; null; null; 20 --> 1
emp[7876] --> [[7876, null, null, null, null, null, null, 20]]
//update table with alias
emp e[:id]{e.ename} = [:n] --> id=7876;n='kuku --> 1
emp[7876] {ename} --> [["kuku"]]
=emp e[?]{e.ename} --> 7876; 'kukuz --> 1
emp[7876] {ename} --> [["kukuz"]]

//child update
emp{empno, job, emp[empno = :1(1)]{job} = [:1(2)]}#(1) --> [[7369, "CLERK", 1], [7499, "SALESMAN", 1], [7521, "SALESMAN", 1], [7566, "MANAGER", 1], [7654, "SALESMAN", 1], [7698, "MANAGER", 1], [7782, "MANAGER", 1], [7788, "ANALYST", 1], [7839, "PRESIDENT", 1], [7844, "SALESMAN", 1], [7876, null, 1], [7900, "CLERK", 1], [7902, "ANALYST", 1], [7934, "CLERK", 1]]

//----DELETE----
salgrade - [?] --> 5 --> 1
//delete table with alias
dummy d - [], DUMMY {DUMMY} + [0], -dummy[], DUMMY {DUMMY} + [0] --> [1, 1, 1, 1]

//negation
-1 - 2 --> -3
-1 - -2 --> 1
-1 - (-2) --> 1

//--ASSIGN EXPR
:v = 5, :s = "kuku" --> [5, "kuku"]

//----BATCH----
SALGRADE {GRADE, LOSAL, HISAL} + [?, ?, ?], emp[?] {ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} = [?, ?, ?, ?, ?, ?, ?], emp[?] --> 6; 10000; 11000; 7876; 'ADAMS;  'CLERK; 7788; '1983-01-12; 1100; null; 20; 7788 --> [1, 1, [[7788, "SCOTT", "ANALYST", 7566, "1982-12-09", 4000.35, null, 20]]]
//two unions in batch
salgrade[?] {grade} + dummy, "A" + " " + "B", dept[dname ~~ ?]{deptno} + dummy, 1 + 3 --> -1; 'kuku% --> [[[0]], "A B", [[0]], 4]
//merging of two results with variables
:r1 = /(dept[dname ~~ ?]{deptno} + dummy)#(1), :r2 = salgrade[?] {grade} + dummy, :r1 | :r2 --> 'acc%; -1 --> [[], [], [[0, 0], [10]]]

//----Scala and DB PROCEDURE CALL
inc_val_5(5) --> [[10]]
inc_val_5(?) --> 10 --> [[15]]
mkString(emp{ename}#(ename), " ", ",") --> ADAMS,ALLEN,BLAKE,CLARK,FORD,JAMES,JONES,KING,MARTIN,MILLER,SCOTT,SMITH,TURNER,WARD

//sequence test
nextval("seq") --> [[10000]]

//----insensitive comparison function test using dialect----
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [9999, "MĀRTIŅŠ ŽVŪKŠĶIS",  "CLERK", 7788, "1983-01-12", 1100, null, 20] --> 1
emp[cmp_i(ename, ?)]{ename} --> 'zvuksk --> [["MĀRTIŅŠ ŽVŪKŠĶIS"]]
emp[cmp_i_any(ename, ?)]{ename} --> 'zvuksk --> [["MĀRTIŅŠ ŽVŪKŠĶIS"]]
emp[cmp_i_start(ename, ?)]{ename} --> 'mar --> [["MARTIN"], ["MĀRTIŅŠ ŽVŪKŠĶIS"]]
emp[cmp_i_end(ename, ?)]{ename} --> 'kskis --> [["MĀRTIŅŠ ŽVŪKŠĶIS"]]
emp[cmp_i(ename, ?)]{ename} --> 'ZVUKSK --> [["MĀRTIŅŠ ŽVŪKŠĶIS"]]
emp[cmp_i_start(ename, ?)]{ename} --> 'MĀR --> [["MĀRTIŅŠ ŽVŪKŠĶIS"]]
emp[cmp_i_start(ename, ?)]{ename} --> 'mār --> [["MĀRTIŅŠ ŽVŪKŠĶIS"]]
emp[cmp_i_exact(ename, ?)]{ename} --> 'martins zvukskis --> [["MĀRTIŅŠ ŽVŪKŠĶIS"]]
emp[cmp_i(ename, ?)]{ename} --> null --> []

//external function test
emp{ename, job, |concat(:1(1), ' ', :1(2))}#(1,2) --> [["ADAMS", "CLERK", "ADAMS CLERK"], ["ALLEN", "SALESMAN", "ALLEN SALESMAN"], ["BLAKE", "MANAGER", "BLAKE MANAGER"], ["CLARK", "MANAGER", "CLARK MANAGER"], ["FORD", "ANALYST", "FORD ANALYST"], ["JAMES", "CLERK", "JAMES CLERK"], ["JONES", "MANAGER", "JONES MANAGER"], ["KING", "PRESIDENT", "KING PRESIDENT"], ["MARTIN", "SALESMAN", "MARTIN SALESMAN"], ["MILLER", "CLERK", "MILLER CLERK"], ["MĀRTIŅŠ ŽVŪKŠĶIS", "CLERK", "MĀRTIŅŠ ŽVŪKŠĶIS CLERK"], ["SCOTT", "ANALYST", "SCOTT ANALYST"], ["SMITH", "CLERK", "SMITH CLERK"], ["TURNER", "SALESMAN", "TURNER SALESMAN"], ["WARD", "SALESMAN", "WARD SALESMAN"]]
emp[deptno = 10]{ename, |echo(:1(1))}#(1) --> [["CLARK", "CLARK"], ["KING", "KING"], ["MILLER", "MILLER"]]
salgrade {losal, hisal, |plus(:1(1), :1(2))}#(1,2) --> [[700, 1200, 1900], [1201, 1400, 2601], [1401, 2000, 3401], [2001, 3000, 5001], [10000, 11000, 21000]]
salgrade {grade, plus(losal, hisal)}#(1) --> [[1, 1900], [2, 2601], [3, 3401], [4, 5001], [6, 21000]]
salgrade {plus(losal, hisal), grade}#(1) --> [[1900, 1], [2601, 2], [3401, 3], [5001, 4], [21000, 6]]
salgrade {grade, plus(losal, hisal), average(losal, hisal)}#(1) --> [[1, 1900, 950], [2, 2601, 1300.5], [3, 3401, 1700.5], [4, 5001, 2500.5], [6, 21000, 10500]]
salgrade {grade, losal, hisal, plus(losal, hisal), average(losal, hisal)}#(1) --> [[1, 700, 1200, 1900, 950], [2, 1201, 1400, 2601, 1300.5], [3, 1401, 2000, 3401, 1700.5], [4, 2001, 3000, 5001, 2500.5], [6, 10000, 11000, 21000, 10500]]
salgrade {plus(losal, hisal), grade, average(losal, hisal), losal, hisal}#(2) --> [[1900, 1, 950, 700, 1200], [2601, 2, 1300.5, 1201, 1400], [3401, 3, 1700.5, 1401, 2000], [5001, 4, 2500.5, 2001, 3000], [21000, 6, 10500, 10000, 11000]]
salgrade {plus(losal * 2, hisal / 2), grade, average(losal + 1000, hisal - 1000), losal, hisal}#(2) --> [[2000, 1, 950, 700, 1200], [3102, 2, 1300.5, 1201, 1400], [3802, 3, 1700.5, 1401, 2000], [5502, 4, 2500.5, 2001, 3000], [25500, 6, 10500, 10000, 11000]]
dept[10] {nopars(), dname} --> [["ok", "ACCOUNTING"]]
dept[10] {dname, nopars()} --> [["ACCOUNTING", "ok"]]

//external function + column query +  child query
dept d [?]{dept_desc(dname, (emp e[d.deptno = e.deptno] {count(*)})) desc, |emp{ename}#(1)} --> 10 --> [["ACCOUNTING (3)", [["CLARK"], ["KING"], ["MILLER"]]]] 
dept d [?]{|emp{ename}#(1), dept_desc(dname, (emp e[d.deptno = e.deptno] {count(*)})) desc} --> 10 --> [[[["CLARK"], ["KING"], ["MILLER"]], "ACCOUNTING (3)"]]
dept d [?]{|emp{ename}#(1), dept_desc(dname, (emp e[d.deptno = e.deptno] {count(*)})) desc, deptno} --> 10 --> [[[["CLARK"], ["KING"], ["MILLER"]], "ACCOUNTING (3)", 10]]

//hierarchical query in from clause select //FIXME: does not return child query result!
(dept{deptno, |emp{ename}})[deptno = 10] --> [[10]]
