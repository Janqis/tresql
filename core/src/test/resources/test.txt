// each line consists of statement to be tested. line format is following:
//<statement> --> [<semicolon separated bind params (array elements are comma separated)>] --> <result as returned from method uniso.query.result.Jsonizer.jsonize(..., Jsonizer.Arrays)>
//INSERT
//DEPTS
DEPT {DEPTNO, DNAME, LOC} + [10, "ACCOUNTING", "NEW YORK"] --> 1
DEPT {DEPTNO, DNAME, LOC} + [20, "RESEARCH",   "DALLAS"] --> 1
DEPT {DEPTNO, DNAME, LOC} + [30, "SALES",      "CHICAGO"] --> 1
DEPT {DEPTNO, DNAME, LOC} + [40, "OPERATIONS", "BOSTON"] --> 1
//EMPS
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7839, "KING",   "PRESIDENT", null, "1981-11-17", 5000, null, 10] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7698, "BLAKE",  "MANAGER",   7839, "1981-05-01",  2850, null, 30] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7782, "CLARK",  "MANAGER",   7839, "1981-06-9", 2450, null, 10] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7566, "JONES",  "MANAGER",   7839, "1981-04-02",  2975, null, 20] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7499, "ALLEN",  "SALESMAN",  7698, "1981-02-20", 1600,  300, 30] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7521, "WARD",   "SALESMAN",  7698, "1981-02-22", 1250,  500, 30] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7654, "MARTIN", "SALESMAN",  7698, "1981-09-28", 1250, 1400, 30] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7844, "TURNER", "SALESMAN",  7698, "1981-09-8", 1500,    0, 30] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7900, "JAMES",  "CLERK",     7698, "1981-12-3", 950, null, 30] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7934, "MILLER", "CLERK",     7782, "1982-01-23", 1300, null, 10] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7788, "SCOTT",  "ANALYST",   7566, "1982-12-09", 3000, null, 20] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7902, "FORD",   "ANALYST",   7566, "1981-12-3", 3000, null, 20] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [7876, "ADAMS",  "CLERK",     7788, "1983-01-12", 1100, null, 20] --> 1
EMP {EMPNO, ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} + [?, ?, ?, ?, ?, ?, ?, ?] --> 7369; 'SMITH; 'CLERK; 7902; 1980-12-17; 800; null; 20 --> 1

//SALGRADES
SALGRADE {GRADE, HISAL, LOSAL} + [1,  700, 1200] --> 1
SALGRADE {GRADE, HISAL, LOSAL} + [2, 1201, 1400] --> 1
SALGRADE {GRADE, HISAL, LOSAL} + [3, 1401, 2000] --> 1
SALGRADE {GRADE, HISAL, LOSAL} + [4, 2001, 3000] --> 1
SALGRADE {GRADE, HISAL, LOSAL} + [5, 3001, 9999] --> 1

//DUMMY
DUMMY {DUMMY} + [0] --> 1

//----SELECT----
dummy --> [[0]]

emp {count(*)} --> [[14]]
emp e/dept d [d.deptno = 20]{empno, ename, dname}#(empno) --> [[7369, "SMITH", "RESEARCH"], [7566, "JONES", "RESEARCH"], [7788, "SCOTT", "RESEARCH"], [7876, "ADAMS", "RESEARCH"], [7902, "FORD", "RESEARCH"]]

//implicit search by primary key
dept[:id] {deptno} #(deptno) --> id=10 --> [[10]]

//in subselect
dept [deptno in (emp{deptno})]#(deptno) --> [[10, "ACCOUNTING", "NEW YORK"], [20, "RESEARCH", "DALLAS"], [30, "SALES", "CHICAGO"]]

//exists correlated subselect
dept d[(emp e[e.deptno = d.deptno])]#(deptno) --> [[10, "ACCOUNTING", "NEW YORK"], [20, "RESEARCH", "DALLAS"], [30, "SALES", "CHICAGO"]]

//outer join
dept d/emp? e[e.deptno != null]#{d.deptno}#(deptno) --> [[10], [20], [30]]

//array binding
emp [empno in(?)]{empno, ename, job}#(empno) --> [7839, 7782] --> [[7782, "CLARK", "MANAGER"], [7839, "KING", "PRESIDENT"]]
emp [ename ~~ ? & empno in(?) & mgr = null & sal = ?]{empno, ename, job} --> 'kin%; [7839, 7782]; 5000  --> [[7839, "KING", "PRESIDENT"]]

//named variables
dept[dname ~~ :name] --> name='acc% --> [[10, "ACCOUNTING", "NEW YORK"]]

//mixed mode - named indexed variables
dept[deptno = ? & dname ~~ :name] --> name='acc%;10 --> [[10, "ACCOUNTING", "NEW YORK"]]

//in subselect with binding
dept[deptno in (emp[sal >= ?]{deptno})] {dname} --> 5000 --> [["ACCOUNTING"]]

//group by, having
emp[sal >= hisal & sal <= losal]salgrade {grade, hisal, losal, count(empno)} (grade, hisal, losal) #(1) --> [[1, 700, 1200, 3], [2, 1201, 1400, 3], [3, 1401, 2000, 2], [4, 2001, 3000, 5], [5, 3001, 9999, 1]]
emp[sal >= hisal & sal <= losal]salgrade {grade, hisal, losal, count(*)} (grade, hisal, losal)^(count(*) > ?)#(1) --> 1 --> [[1, 700, 1200, 3], [2, 1201, 1400, 3], [3, 1401, 2000, 2], [4, 2001, 3000, 5]] 

//two level query: salgrade -> emp
salgrade[?] {grade, hisal, losal, |emp[sal >= :1(2) & sal <= :1(3)]{ename, sal}} --> 5 --> [[5, 3001, 9999, [["KING", 5000.00]]]]

//three level query: dept -> salgrade -> emp
dept{deptno, dname, |emp[sal >= hisal & sal <= losal]salgrade[deptno = :1(1)] {grade, hisal, losal, count(empno) empcount, |emp/dept[sal >= :1(2) & sal <= :1(3) & emp.deptno = :2(1)]{ename, emp.deptno, dname, sal}#(empno) emps} (grade, hisal, losal) #(1) salgrades}#(deptno) --> [[10, "ACCOUNTING", [[2, 1201, 1400, 1, [["MILLER", 10, "ACCOUNTING", 1300.00]]], [4, 2001, 3000, 1, [["CLARK", 10, "ACCOUNTING", 2450.00]]], [5, 3001, 9999, 1, [["KING", 10, "ACCOUNTING", 5000.00]]]]], [20, "RESEARCH", [[1, 700, 1200, 2, [["SMITH", 20, "RESEARCH", 800.00], ["ADAMS", 20, "RESEARCH", 1100.00]]], [4, 2001, 3000, 3, [["JONES", 20, "RESEARCH", 2975.00], ["SCOTT", 20, "RESEARCH", 3000.00], ["FORD", 20, "RESEARCH", 3000.00]]]]], [30, "SALES", [[1, 700, 1200, 1, [["JAMES", 30, "SALES", 950.00]]], [2, 1201, 1400, 2, [["WARD", 30, "SALES", 1250.00], ["MARTIN", 30, "SALES", 1250.00]]], [3, 1401, 2000, 2, [["ALLEN", 30, "SALES", 1600.00], ["TURNER", 30, "SALES", 1500.00]]], [4, 2001, 3000, 1, [["BLAKE", 30, "SALES", 2850.00]]]]], [40, "OPERATIONS", []]]

//two level query, child query in the midst of parent query columns
dept[?]{loc, deptno, |emp[deptno = :1(2)] {job}, dname} --> 10 --> [["NEW YORK", 10, [["PRESIDENT"], ["MANAGER"], ["CLERK"]], "ACCOUNTING"]]

//union
dept[?]{deptno} ++ emp[?]{empno} ++ salgrade[?]{grade} --> 10;7839;1 --> [[10], [7839], [1]]
/(dept{deptno} + salgrade{grade}) ~#(1) --> [[40], [30], [20], [10], [5], [4], [3], [2], [1]]

//intersect
dept{deptno} && emp{empno} --> []
dept d[d.deptno = d1.deptno](dept{deptno} && emp[sal >= ?]{deptno}) d1 {deptno, dname} --> 5000 --> [[10, "ACCOUNTING"]]

//product, multiple col sorting
dept[]salgrade{dname, grade}#(1,loc)~#(2) --> [["ACCOUNTING", 5], ["ACCOUNTING", 4], ["ACCOUNTING", 3], ["ACCOUNTING", 2], ["ACCOUNTING", 1], ["OPERATIONS", 5], ["OPERATIONS", 4], ["OPERATIONS", 3], ["OPERATIONS", 2], ["OPERATIONS", 1], ["RESEARCH", 5], ["RESEARCH", 4], ["RESEARCH", 3], ["RESEARCH", 2], ["RESEARCH", 1], ["SALES", 5], ["SALES", 4], ["SALES", 3], ["SALES", 2], ["SALES", 1]]
dept[]salgrade[]dummy{dname, grade, dummy}#(1,loc)~#(2) --> [["ACCOUNTING", 5, 0], ["ACCOUNTING", 4, 0], ["ACCOUNTING", 3, 0], ["ACCOUNTING", 2, 0], ["ACCOUNTING", 1, 0], ["OPERATIONS", 5, 0], ["OPERATIONS", 4, 0], ["OPERATIONS", 3, 0], ["OPERATIONS", 2, 0], ["OPERATIONS", 1, 0], ["RESEARCH", 5, 0], ["RESEARCH", 4, 0], ["RESEARCH", 3, 0], ["RESEARCH", 2, 0], ["RESEARCH", 1, 0], ["SALES", 5, 0], ["SALES", 4, 0], ["SALES", 3, 0], ["SALES", 2, 0], ["SALES", 1, 0]]

//all cols: *
dept[10]{*} --> [[10, "ACCOUNTING", "NEW YORK"]]
dept[10]{*, |emp[deptno = :1(1)]{ename}} --> [[10, "ACCOUNTING", "NEW YORK", [["KING"], ["CLARK"], ["MILLER"]]]]
dept[?]{|emp[?]{ename}, *} --> 10;7839 --> [[[["KING"]], 10, "ACCOUNTING", "NEW YORK"]]

//single quoted string
dept[dname ~ 'ACC%'] --> [[10, "ACCOUNTING", "NEW YORK"]]

//optional binding
dept[deptno = :id?] {deptno} #(deptno) --> [[10], [20], [30], [40]]
dept[:id?] {deptno} #(deptno) --> id=10 --> [[10]]
dept[:id?] {deptno} #(deptno) --> [[10], [20], [30], [40]]
dept[:id? & dname ~~ :name? & loc ~~ :loc?] --> loc = 'bost% --> [[40, "OPERATIONS", "BOSTON"]]
dept[(:id? & (dname ~~ :name? & (loc ~~ :loc?)))] --> loc = 'bost% --> [[40, "OPERATIONS", "BOSTON"]]

//distinct
/(dept{deptno} ++ dept{deptno})#{deptno} --> [[10], [20], [30], [40]]

//----UPDATE----
emp[hiredate >= ? & hiredate <= ?]{sal} = [sal + ?] --> '1982-01-23;'1982-12-09;1000.35 --> 2
emp[hiredate >= ? & hiredate <= ?]{sal} ~#(1) --> '1982-01-23;'1982-12-09 --> [[4000.35], [2300.35]]
emp[?] {ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} = [?, ?, ?, ?, ?, ?, ?] --> 7876; null; null; null; null; null; null; null --> 1
emp[7876] --> [[7876, null, null, null, null, null, null, null]]

//----DELETE----
salgrade - [?] --> 5 --> 1

//--ASSIGN EXPR
:v = 5, :s = "kuku" --> [5, "kuku"]

//----BATCH----
SALGRADE {GRADE, HISAL, LOSAL} + [?, ?, ?], emp[?] {ENAME, JOB, MGR, HIREDATE, SAL, COMM, DEPTNO} = [?, ?, ?, ?, ?, ?, ?], emp[?] --> 6; 10000; 11000; 7876; 'ADAMS;  'CLERK; 7788; '1983-01-12; 1100; null; 20; 7788 --> [1, 1, [[7788, "SCOTT", "ANALYST", 7566, "1982-12-09", 4000.35, null, 20]]]
//two unions in batch
salgrade[?] {grade} + dummy, "A" + " " + "B", dept[dname ~~ ?]{deptno} + dummy, 1 + 3 --> -1; 'kuku% --> [[[0]], "A B", [[0]], 4]
//merging of two results with variables
:r1 = /(dept[dname ~~ ?]{deptno} + dummy)#(1), :r2 = salgrade[?] {grade} + dummy, :r1 | :r2 --> 'acc%; -1 --> [[], [], [[0, 0], [10]]]
